#+ summary: Get a human-readable summary for a given nanopublication.
#+ method: GET
#+ pagination: 1000
#+ endpoint: https://query.knowledgepixels.com/repo/full
#+ endpoint_in_url: False

prefix np: <http://www.nanopub.org/nschema#>
prefix npa: <http://purl.org/nanopub/admin/>
prefix npx: <http://purl.org/nanopub/x/>
prefix xsd: <http://www.w3.org/2001/XMLSchema#>
prefix dct: <http://purl.org/dc/terms/>
prefix oa: <http://www.w3.org/ns/oa#>
prefix rdfs: <http://www.w3.org/2000/01/rdf-schema#>
prefix nt: <https://w3id.org/np/o/ntemplate/>

select distinct ?np ?user ?thing ?label (group_concat(?statement; separator=' ') as ?statements) ?date where {
  graph npa:graph {
    bind(replace(str(?_np_iri), "^.*[^a-zA-Z0-9-_](RA[a-zA-Z0-9-_]{43})$", "$1") as ?ac)
    ?np npa:artifactCode ?ac .
    optional {<x:> <x:> ?_np_iri}  # for grlc to pick up the placeholder
    ?np npa:hasHeadGraph ?h .
    ?np dct:created ?date .
    ?np npa:hasValidSignatureForPublicKey ?pubkey .
    
  }
  graph ?h {
    ?np np:hasAssertion ?a .
    ?np np:hasPublicationInfo ?i .
  }
  graph ?a {
    ?subj ?rel ?obj .
    optional { ?thing rdfs:label ?thingLabel. }
  }
  graph ?i {
    ?np dct:creator ?user .
    ?np nt:wasCreatedFromTemplate ?template .
  }
  optional { graph ?i { ?np npx:introduces ?thing . } }
  optional { graph ?i { ?np rdfs:label ?nanopubLabel . } }
  ?template np:hasAssertion ?templatea .
  optional { graph ?i { ?subj nt:hasLabelFromApi ?subjlabel1 } }
  optional { graph ?i { ?rel nt:hasLabelFromApi ?rellabel1 } }
  optional { graph ?i { ?obj nt:hasLabelFromApi ?objlabel1 } }
  optional { graph ?templatea { ?subj rdfs:label ?subjlabel2 } }
  optional { graph ?templatea { ?rel rdfs:label ?rellabel2 } }
  optional { graph ?templatea { ?obj rdfs:label ?objlabel2 } }
  bind(coalesce(?subjlabel1, ?subjlabel2) as ?subjlabel)
  bind(coalesce(?rellabel1, ?rellabel2) as ?rellabel)
  bind(coalesce(?objlabel1, ?objlabel2) as ?objlabel)
  bind(coalesce(replace(str(?subjlabel), " - .*", ""), replace(str(?subj), "^.*[/#]([^/#]*)$", "$1")) as ?subjx)
  bind(coalesce(replace(str(?rellabel), " - .*", ""), replace(str(?rel), "^.*[/#]([^/#]*)$", "$1")) as ?relx)
  bind(coalesce(replace(str(?objlabel), " - .*", ""), replace(str(?obj), "^.*[/#]([^/#]*)$", "$1")) as ?objx)
  bind(concat(?subjx, " ", ?relx, " ", ?objx, ".") as ?statement)
  bind(coalesce(str(?nanopubLabel), str(?thingLabel)) as ?label)
} group by ?np ?user ?thing ?label ?date
